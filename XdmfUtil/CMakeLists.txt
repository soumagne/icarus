PROJECT("XDMF_UTIL")

CMAKE_MINIMUM_REQUIRED (VERSION 2.6)

IF(COMMAND CMAKE_POLICY)
  CMAKE_POLICY(SET CMP0003 NEW)
ENDIF(COMMAND CMAKE_POLICY)

CONFIGURE_FILE(
  ${CMAKE_CURRENT_SOURCE_DIR}/XdmfUtilconfig.h.in
  ${CMAKE_CURRENT_BINARY_DIR}/XdmfUtilconfig.h)
  
#--------------------------------------------------
# Load ParaView env
#--------------------------------------------------
IF(NOT USE_H5FD_DSM)
  IF(NOT PARAVIEW_LOADED)
    FIND_PACKAGE(ParaView REQUIRED)
    INCLUDE(${PARAVIEW_USE_FILE})
    SET(PARAVIEW_LOADED 1)
  ELSE(NOT PARAVIEW_LOADED)
    IF (PARAVIEW_BUILD_QT_GUI)
      INCLUDE(${QT_USE_FILE})
    ENDIF (PARAVIEW_BUILD_QT_GUI)
    INCLUDE_DIRECTORIES(
      ${PARAVIEW_INCLUDE_DIRS}
      ${PARAVIEW_KWSYS_INCLUDE_DIRS}
      ${VTK_INCLUDE_DIR}
      ${MPI_INCLUDE_PATH}
    )
    IF (VTK_USE_SYSTEM_LIBXML2)
      INCLUDE_DIRECTORIES(
        ${LIBXML2_INCLUDE_DIR}
      )
    ELSE (VTK_USE_SYSTEM_LIBXML2)
      INCLUDE_DIRECTORIES(
        ${ParaView_BINARY_DIR}/VTK/Utilities/vtklibxml2
      )
    ENDIF (VTK_USE_SYSTEM_LIBXML2)
  ENDIF(NOT PARAVIEW_LOADED)
ENDIF(NOT USE_H5FD_DSM)

INCLUDE_DIRECTORIES(
  ${CMAKE_CURRENT_SOURCE_DIR} 
  ${CMAKE_CURRENT_SOURCE_DIR}/h5dump
  ${CMAKE_CURRENT_BINARY_DIR}
  )

SET(XdmfUtil_SRCS
  ${CMAKE_CURRENT_SOURCE_DIR}/XdmfGenerator.cxx
  ${CMAKE_CURRENT_SOURCE_DIR}/XdmfHDFDOM.cxx
  ${CMAKE_CURRENT_SOURCE_DIR}/FileSeriesFinder.cxx
  ${CMAKE_CURRENT_SOURCE_DIR}/XdmfDump.cxx
  ${CMAKE_CURRENT_SOURCE_DIR}/h5dump/h5dump.cxx
  ${CMAKE_CURRENT_SOURCE_DIR}/h5dump/h5tools.cxx
  ${CMAKE_CURRENT_SOURCE_DIR}/h5dump/h5tools_str.cxx
  ${CMAKE_CURRENT_SOURCE_DIR}/h5dump/h5tools_utils.cxx
  ${CMAKE_CURRENT_SOURCE_DIR}/h5dump/h5tools_ref.cxx
  ${CMAKE_CURRENT_SOURCE_DIR}/h5dump/h5tools_type.cxx
  ${CMAKE_CURRENT_SOURCE_DIR}/h5dump/h5trav.cxx
  )

IF(VTK_BUILD_SHARED_LIBS OR BUILD_SHARED_LIBS)
  SET(XdmfUtil_LIBTYPE SHARED)
ELSE(VTK_BUILD_SHARED_LIBS OR BUILD_SHARED_LIBS)
  SET(XdmfUtil_LIBTYPE STATIC)
ENDIF(VTK_BUILD_SHARED_LIBS OR BUILD_SHARED_LIBS)

ADD_LIBRARY(XdmfUtil ${XdmfUtil_LIBTYPE} ${XdmfUtil_SRCS})
TARGET_LINK_LIBRARIES(XdmfUtil Xdmf vtksys)

ADD_EXECUTABLE(XdmfGenerate ${CMAKE_CURRENT_SOURCE_DIR}/XdmfGenerate.cxx)
TARGET_LINK_LIBRARIES(XdmfGenerate XdmfUtil)

#--------------------------------------------------
# Install
#--------------------------------------------------
INSTALL(
  TARGETS
    XdmfUtil XdmfGenerate
  DESTINATION 
    "${CMAKE_INSTALL_PREFIX}"
)

